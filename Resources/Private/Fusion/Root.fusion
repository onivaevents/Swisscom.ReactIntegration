prototype(Swisscom.ReactIntegration:ReactScripts) < prototype(Neos.Fusion:Component) {
    // Public API

    // Paths to the COMPILED resources (used when productionMode == true)
    jsPath = 'resource://Your.Package/Public/static/js'
    cssPath = 'resource://Your.Package/Public/static/css'

    // Path to the development server to extract the JavaScript from. This URL must be reachable
    // from within PHP!
    developmentServerUrl = 'http://127.0.0.1:3000'

    // In case the dev server is accessed through a different host from the browser,
    // add this URL here.
    publicDevelopmentServerUrl = ${this.developmentServerUrl}

    // Private
    _scriptUrls = ${Configuration.setting("Swisscom.ReactIntegration.productionMode") ? Swisscom.ReactIntegration.getCompiledReactScriptUris(this.jsPath, 'js') : Swisscom.ReactIntegration.getReactDevelopmentScriptUris(this.developmentServerUrl, this.publicDevelopmentServerUrl)}
    // We only need to include css in production because react-scripts already includes css via js in development.
    _cssUrls = ${Configuration.setting("Swisscom.ReactIntegration.productionMode") ? Swisscom.ReactIntegration.getCompiledReactScriptUris(this.cssPath, 'css') : []}

    renderer = afx`
        <Neos.Fusion:Loop items={props._cssUrls} itemName="cssUrl">
            <link rel="stylesheet" type="text/css" href={cssUrl}/>
        </Neos.Fusion:Loop>
        <Neos.Fusion:Loop items={props._scriptUrls} itemName="scriptUrl">
            <script type="application/javascript" src={scriptUrl}></script>
        </Neos.Fusion:Loop>
    `
}
